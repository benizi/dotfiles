setopt promptsubst
local _i_am_admin=false
if [ -n "$INCYG" -a "$USERNAME" = "Administrator" ] || [ "$UID" = "0" ] ; then
	_i_am_admin=true
fi
if $_i_am_admin ; then
	_ps_color='${PSCOLORALL-${PSCOLORROOT-$UNDERLINE}}'
else
	_ps_color='${PSCOLORALL-$PSCOLORNORM}'
fi
$_i_am_admin && _pschar='#' || _pschar='$'
local _screen=""
if [ -n "$STY" ] ; then
	_screen="$STY"
	[[ $_screen = [0-9]*.* ]] && _screen="${_screen[(ws:.:)2,-1]}"
	local _host=$(print -P '%m')
	[[ $_screen = *.$_host ]] && _screen=$_screen:r
	_screen="[$_screen]"
fi
PROMPT='%{${PSCOLOR}'$_ps_color'%}%n@%m'$_screen' %~'$_pschar'%{$NOCOLOR%} '

TITLESEP='│' # ceçi n'est pas un pipe
[ -n "$INCYG" ] && TITLESEP='|'
function fancytitle () {
	local host c=$TITLESEP
	host="%m $c "
	case "$DISPLAY" in
		:0*|localhost:*) [ -z "$SSH_CLIENT" ] && host="" ;;
	esac
	$_i_am_admin && extra="ROOT $c " || extra=""
	printf "${TITLE}%s%s${ENDTITLE}" $1 "$(print -nP "$extra$host"'%~ $c %n${STY+ $c $STY}')"
}
function term_with_title () {
	if [ $SHLVL -eq 1 -a "$TERM" = linux ] ; then return 1 ; fi
	if [ -n "$NOTITLE" ] ; then return 1 ; fi
	return 0
}
function preexec () {
	term_with_title || return
	fancytitle "$1 $TITLESEP "
}
function precmd () {
	term_with_title || return
	fancytitle ""
}
