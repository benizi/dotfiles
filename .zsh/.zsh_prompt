local simple_prompt
unset simple_prompt

case $TERM in
  9term) simple_prompt=true ;;
esac

if (( $+simple_prompt )) ; then
  PS1='%n@%m %~%% '
  return
fi

__light_terminal () {
  [[ $TERM != (st*|konsole*|rxvt-unicode-24bit*) ]]
}

__i_am_admin () {
  local admin=1
  [ -n "$INCYG" -a "$USERNAME" = "Administrator" ] && admin=0
  [ -n "$INCYG" ] && [[ "$(id)" = *,0'(root)',* ]] && admin=0
  [ "$UID" = "0" ] && admin=0
  return $admin
}

__prompt_screen () {
  local _screen=''
  if [ -n "$STY" ] ; then
    _screen="$STY"
    [[ $_screen = [0-9]*.* ]] && _screen="${_screen[(ws:.:)2,-1]}"
    local _host=$(print -P '%m')
    [[ $_screen = *.$_host ]] && _screen=$_screen:r
  elif [ -n "$DTACH" ] ; then
    _screen=${DTACH:t}
  fi
  printf %s $_screen
}

__prompt_vcs () {
  local -a test_git types
  local -A vcs
  test_git=( $PWD(N) )
  other_vcs=()
  local -A added
  local n=0 git_dir= o
  while (( $#test_git && n < 20 )) ; do
    local d=$test_git[1]
    # avoid issues with Cygwin/MSYS treating // specially
    [[ $d = / ]] && d=''
    shift test_git
    n=$(( n + 1 ))
    for o in .hg .svn CVS ; do
      local other_type=${o:l:s/./}
      local other_dir=$d/$o
      (( $+vcs[$other_type] )) && continue
      [[ -d $other_dir ]] || continue
      # skip if not highest level of repo for hierarchically-littered systems
      [[ $other_type = (svn|cvs) ]] && [[ -d $other_dir:h:h/$o:t ]] && continue
      vcs[$other_type]=${:-"$(cd -q $d ; print -P '%~')"}
      types+=( $other_type )
    done
    [[ -d $d/.git ]] && git_dir=$d/.git && break
    local -a add
    add=( $d:h $d/./..(+A,N) )
    for d in $add ; do
      (( $+added[(e)$d] )) && continue
      test_git+=( $d )
      added[$d]=1
    done
  done
  if (( $#git_dir )) ; then
    local -a pretty_git
    local dir=$(cd -q $git_dir:h ; print -P '%~')
    [[ $dir = $(print -P '%~') ]] || pretty_git+=( $dir )
    local branch="$(git --git-dir=$git_dir branch | grep '^[*]' | cut -c3-)"
    [[ "$branch" = master ]] || pretty_git+=( $branch )
    (( $#pretty_git )) || pretty_git+=( . )
    (( $#pretty_git > 1 && ${#pretty_git[1]} > 15 )) && pretty_git[1]=''
    vcs[git]=${(j.:.)pretty_git}
    types+=( git )
  fi
  for o in $types ; do
    printf '%s:%s\n' $o $vcs[$o]
  done
}

__prompt_ruby_manager () {
  if (( $+ruby_manager )) ; then
    case $ruby_manager in
      chruby) printf %s "chruby:${RUBY_ROOT:t}" ;;
      prb) printf %s "prb:$(prb version --short)" ;;
      rbenv) printf %s "$(rbenv version-name)" ;;
      rvm) printf %s "rvm:$(rvm current)" ;;
      rbfu) printf %s ${RBFU_RUBY_VERSION:-rbfu@system} ;;
      *) printf %s "$ruby_manager:?" ;;
    esac
  fi
}

__setup_prompt () {
  setopt promptsubst

  (( $+__prompt_color )) || __prompt_color=( f:231 k:124 b: )
  local ps_char='$'
  (( $+termcap[Co] )) && (( $termcap[Co] > 8 )) || __prompt_color=()
  if __i_am_admin ; then
    __prompt_color+=( s: )
    ps_char='#'
  fi

  local screen="$(__prompt_screen)"
  local -a vcs
  vcs=( "${(f):-"$(__prompt_vcs)"}" )

  local x11 deleted socks fail_couchdb current_dir virtenv_info
  unset x11 deleted socks fail_couchdb virtenv_info
  local ruby_info="$(__prompt_ruby_manager)"
  (( $+VIRTUAL_ENV )) && virtenv_info=$VIRTUAL_ENV:t

  [[ ! -e $PWD ]] && deleted=true
  [[ -e ~$owner/failed-couchdb ]] && fail_couchdb=true

  if [[ "$DISPLAY" = localhost:[[:digit:]]*.0 ]] ; then
    (( ${${DISPLAY#localhost:}%.0} > 9 )) && x11=ssh
  elif (( $+DISPLAY )) && [[ "$DISPLAY" != :0 ]] ; then
    x11=$DISPLAY
  fi

  if [[ "$LD_PRELOAD" = *proxychains* ]] && [[ -n "$PROXY_SOCKS5" ]] ; then
    socks=$PROXY_SOCKS5
  fi

  current_dir=${${(%):-%d}/$HOME/${:-'~'}}
  if [[ ${(%):-%~} = */* ]] ; then
    current_dir=%~
  elif [[ $current_dir != ${(%):-%~} ]] ; then
    current_dir="%~ [$current_dir]"
  fi

  typeset -a top_row middle_row bottom_row
  map_att () {
    local att=$1
    local -A map
    __light_terminal && map=( f k k f )
    (( $+map[$att] )) && att=$map[$att]
    printf %s $att
  }

  add.top_row add.middle_row add.bottom_row () {
    local arg
    local -a rest
    local -A atts
    atts=( f 15 )
    while (( $# )) ; do
      local arg=$1
      shift
      case $arg in
        :norm) atts=() ;;
        f:*|k:*|?:) atts[${arg%%\:*}]=${arg#*:} ;;
        *) rest+=( $arg $@ ) ; break ;;
      esac
    done
    (( $#rest )) || return 0
    local att= val= c_pre= c_post=
    for att in ${(k)atts} ; do
      local key="$(map_att $att)"
      val=$atts[$att]
      c_pre+=%${key:u}
      (( $#val )) && c_pre+='{'$val'}'
      c_post+=%$key
    done
    eval "${0:e}+=( $c_pre${(@q)^rest}$c_post )"
  }

  add.top_row k:22 $current_dir
  add.top_row $__prompt_color %n@%m
  add.top_row k:128 $screen
  add.top_row k:blue $^vcs[1,1]
  add.top_row f:yellow $^vcs[2,-1]
  add.top_row k:red $ruby_info
  (( $+NVM_VERSION )) && add.top_row k:128 ${NVM_VERSION#v}
  (( $+gvm_go_name )) && add.top_row k:166 ${gvm_go_name#go}
  add.top_row k:22 $virtenv_info
  (( $+socks )) && add.top_row k:22 "socks:$socks"
  (( $+x11 )) && add.top_row k:27 "display $x11"
  add.top_row k:17 '%D{%Y-%m-%d %H:%M:%S}'
  add.top_row k:68 $TERM

  if (( $__prompt_return )) ; then
    local escaped_command=${${${__last_command%%$'\n'}:gs/[$]/\\\$}:gs/[%]/%%}
    add.middle_row k:208 $escaped_command' -> %?'
  fi

  (( $+deleted )) && add.bottom_row k:red '!deleted!'
  (( $+fail_couchdb )) && add.bottom_row k:red '!CouchDB!'
  (( $+incognito_mode )) && add.bottom_row k:208 '!!!'
  add.bottom_row :norm $ps_char

  local -a lines
  if (( $+hi_color )) ; then
    lines=(
      ''
      '╭─╴'${(j:╶╴:)top_row}' ╼'
      '│ '$^middle_row
      '╰╴'${(j:╶╴:)bottom_row}' '
    )
  else
    lines=(
      ''
      '/- '${(j: - :)top_row}
      '| '$^middle_row
      '\\- '${(j: - :)bottom_row}' '
    )
  fi
  PROMPT=${(F)lines}

  unfunction map_att add.top_row add.middle_row add.bottom_row
}

TITLESEP='│' # ceçi n'est pas un pipe
(( $+INCYG || $+STY )) && TITLESEP='|'
[[ $TERM = (screen|cygwin)* ]] && TITLESEP='|'
fancytitle () {
  (( $+terminfo[tsl] )) || return 0
  [[ -n "$NOTITLE" ]] && return 0
  local host c=$TITLESEP title
  host="%m $c "
  case "$DISPLAY" in
    :0*|localhost:*) [ -z "$SSH_CLIENT" ] && host="" ;;
  esac
  local extra=
  __i_am_admin && extra="ROOT $c "
  title="$1$(print -nP "$extra$host"'%~ $c %n${STY+ $c $STY}')"
  [[ $title = ?(#c100,) ]] && title=${title[1,88]}...
  echoti tsl
  printf %s $title
  echoti fsl
}

typeset -A __terminal_seqs

__reset_cursor () {
  if (( $+__terminal_seqs[reset] )) ; then
    printf '%s' $__terminal_seqs[reset]
  fi
}

preexec () {
  __last_command=$1
  fancytitle "$1 $TITLESEP "
  __reset_cursor
}

precmd () {
  __prompt_return=$?
  __setup_prompt
  fancytitle ""
}

() {
  zle-line-init zle-keymap-select () {
    local normal=1 insert
    __light_terminal && insert=0 || insert=15
    if [[ $TERM = rxvt* ]] ; then
      __terminal_seqs+=( cmd $'\e]12;'$normal$'\a' )
      __terminal_seqs+=( ins $'\e]12;'$insert$'\a' )
    elif [[ $TERM = (st*|*xterm*) ]] ; then
      __terminal_seqs+=( cmd $'\e[2 q' )
      __terminal_seqs+=( ins $'\e[6 q' )
      __terminal_seqs[reset]=$__terminal_seqs[cmd]
    elif (( $+terminfo[cvvis] && $+terminfo[cnorm] )) ; then
      __terminal_seqs+=( cmd $terminfo[cvvis] )
      __terminal_seqs+=( ins $terminfo[cnorm] )
    else
      return
    fi
    case $KEYMAP in
      vicmd) print -rn -- $__terminal_seqs[cmd] ;;
      main|viins) print -rn -- $__terminal_seqs[ins] ;;
    esac
  }
  # need line-init for cancelled commands
  zle -N zle-line-init
  zle -N zle-keymap-select
}
