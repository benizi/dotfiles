setopt promptsubst
_i_am_admin=false
if [ -n "$INCYG" -a "$USERNAME" = "Administrator" ] || [ "$UID" = "0" ] ; then
	_i_am_admin=true
fi
if $_i_am_admin ; then
	_ps_color='${PSCOLORALL-${PSCOLORROOT-$UNDERLINE}}'
else
	_ps_color='${PSCOLORALL-$PSCOLORNORM}'
fi
$_i_am_admin && _pschar='#' || _pschar='$'
PROMPT='%{${PSCOLOR}'$_ps_color'%}%n@%m %~'$_pschar'%{$NOCOLOR%} '

TITLESEP='│' # ceçi n'est pas un pipe
[ -n "$INCYG" ] && TITLESEP='|'
function fancytitle () {
	local host c=$TITLESEP
	case "$DISPLAY" in
		:0*|localhost:*) host="" ;;
		*) host="%m $c " ;;
	esac
	$_i_am_admin && extra="ROOT $c " || extra=""
	printf "${TITLE}%s%s${ENDTITLE}" $1 "$(print -nP "$extra$host"'%~ $c %n${STY+ $c $STY}')"
}
function preexec () {
	if [ $SHLVL -eq 1 -a "$TERM" = "linux" ] ; then return ; fi
	[[ -z "$NOTITLE" ]] || return
	fancytitle "$1 $TITLESEP "
}
function precmd () {
	if [ $SHLVL -eq 1 -a "$TERM" = "linux" ] ; then return ; fi
	[[ -z "$NOTITLE" ]] || return
	fancytitle ""
}
