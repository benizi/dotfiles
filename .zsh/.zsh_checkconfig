map() {
  local cmd=$1
  shift
  local arg
  for arg
  do $cmd $arg
  done
}

check_command_installed() {
  local cmd=$1
  (( $+commands[$cmd] )) || warn "$cmd is not installed"
}

check_config_exists() {
  local cfg=$1
  if [[ ! -e ~/$cfg ]]
  then warn "$cfg doesn't exist"
  elif [[ ! -L ~/$cfg ]]
  then warn "$cfg isn't a link"
  fi
}

check_config_exists_graphical() {
  (( ! $+DISPLAY )) || check_config_exists "$@"
}

() {
  map check_command_installed dt

  local -a configs

  configs=(
    .config/nixpkgs/config.nix
    .emacs.d
    .gitconfig
    .githooks
    .ghc/ghci.conf
    .haskeline
    .tmux.conf
    .tmuxinator
    .vimrc
    .zshenv
    bin
  )

  map check_config_exists $configs

  configs=(
    .XCompose
    .XCompose.d
    .Xdefaults
    .urxvt/ext
    .xmonad
  )

  map check_config_exists_graphical $configs
}
