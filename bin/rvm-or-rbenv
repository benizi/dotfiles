#!/bin/zsh
set -o magic_equal_subst

typeset -A r_dirs
r_dirs[rvm]=~/.rvm
r_dirs[rvm_bak]=~/rvm-is-evil
r_dirs[rbenv]=~/.rbenv
r_dirs[rbenv_bak]=~/.rbenv.bak

current () {
	local prog
	for prog in ${(k)r_dirs} ; do
		[[ $prog = *_bak ]] && continue
		[[ -e $r_dirs[$prog] ]] && using=true || using=false
		if (( $+short )) ; then
			$using && echo $prog
		else
			$using || printf "%s " not
			echo using $prog
		fi
	done
}

activate () {
	local prog=$1
	move_dir ${prog}_bak $prog
}

backup () {
	local prog=$1
	move_dir $prog ${prog}_bak
}

move_dir () {
	local src=$r_dirs[$1] dst=$r_dirs[$2]
	if [[ -e $src ]] && [[ -e $dst ]] ; then
		echo Both $src and $dst exist
	elif [[ -e $src ]] ; then
		echo moving $src $dst
		mv $src $dst
	elif [[ -e $dst ]] ; then
		echo $dst already exists
	else
		echo $src does not exist
	fi
}

if (( $#argv )) ; then
	for arg ; do
		case $arg in
			rvm) backup rbenv ; activate rvm ;;
			rb*) backup rvm ; activate rbenv ;;
			-s|*short) short=true ;;
			*) echo "USAGE: $0 [rvm | rbenv]" ;;
		esac
	done
fi

current
