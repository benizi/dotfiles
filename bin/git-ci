#!/usr/bin/env perl
use strict;
use warnings;
use POSIX 'strftime';

my $author = $0 =~ /cit$/;
my $commit = 0;
@ARGV = grep {!(
  /^--both/ ? ($commit = 1) :
0)} @ARGV;

my @times;

my @changed = grep length, split /\x00/, qx{git diff -z --cached --name-only};
my @files = (@ARGV, @changed);

push @times, map +(stat)[9], grep -f, @files;
chdir $ENV{GIT_PREFIX} if $ENV{GIT_PREFIX};
push @times, map +(stat)[9], grep -f, @files;

# git commit --all
push @times, map +(stat)[9], split /\x00/, qx{git ls-files -z -m} unless @times;

my $tz = strftime "%z", localtime;
my $latest = (sort { $b <=> $a } @times)[0] || time;

my %enabled = (GIT_AUTHOR_DATE => $author, GIT_COMMITTER_DATE => $commit);
$ENV{$_} = "$latest $tz" for grep $enabled{$_}, keys %enabled;

system { 'git' } 'git', 'check-email';
system { 'git' } 'git', 'commit', @ARGV;
