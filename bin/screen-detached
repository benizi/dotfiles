#!/bin/zsh

usage() {
	cat <<USAGE
Usage:
  ${0:t} [options] command [args]
Options:
  -n/--dry/--dry-run       Just print the command that would be run
  -h/--help                Print this usage information and exit
  -1/--single/--one        Don't start a new process if one exists
  -a/--attach/--no-detach  Don't detach at the outset
    or set the STAYATTACH env var
  -S NAME / --name NAME    Set the session name [default: \$command]
USAGE
	exit
}

zparseopts -D \
	{n,-dry{,-run}}=dry \
	{h,-help}=help \
	{1,-{single,one}}=singleton \
	{S,-name}:=name \
	{-no-d{,etach},a,-attach{,ed}}=attach

(( $#dry )) && doit=echo || doit=exec
(( $#attach )) && unset detach || detach=true
(( $#singleton )) && checkrunning=true || unset checkrunning
(( $#name )) && sess=${name[2]} || sess=$1

if (( $#help )) || (( ! $#argv )) ; then
	usage
fi

screenargs=()
args=( "$@" )

if (( $+commands[dt] )) ; then # prefer dt by a wide margin...
	dt -S $sess --check && exit $(( 1 - $+checkrunning ))
	(( ! $+STAYATTACH )) && (( $+detach )) && screenargs=( -n $screenargs )
	$doit dt -S $sess $screenargs -- $args
else
	[[ -e /run/screens ]] && sudo chmod g-w /run/screens
	(( $+checkrunning )) && screen-running $sess && exit 0
	(( ! $+STAYATTACH )) && (( $+detach )) && screenargs=( -d -m )
	$doit screen -S $sess "${screenargs[@]}" -- "${args[@]}"
fi
