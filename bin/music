#!/bin/zsh

imedia=org.mpris.MediaPlayer2.Player
spotify=
mpc=

running() {
  case $1 in
    (spotify)
      spotifydbus \
        org.freedesktop.DBus.Properties.Get \
        string:$imedia \
        string:PlaybackStatus \
        &>/dev/null
      ;;
    (mpc|mpd)
      mpc status &>/dev/null
      ;;
  esac
}

spotifydbus() {
  dbus-send \
    --print-reply=literal \
    --dest=com.spotify.qt \
    /org/mpris/MediaPlayer2 \
    "$@"
}

spotify_ctl() {
  local cmd=$1
  shift
  case $cmd in
    (play|pause|toggle) cmd=PlayPause ;;
    (prev) cmd=Previous ;;
    (next|*) cmd=${cmd:0:1:u}${cmd:1} ;;
  esac
  spotifydbus $imedia.$cmd "$@"
}

mpc_ctl() {
  case $1 in
    (play|pause) mpc toggle ;;
    (stop) mpd --kill ;;
    (next|prev|toggle) mpc $1 ;;
  esac
}

findplayer() {
  ! running spotify || spotify=spotify
  ! running mpc || mpc=mpc
  [[ $spotify$mpc != spotifympc ]] || mpc=
}

while test $# -gt 0
do
  case $1 in
    (--spotify) spotify=spotify ; mpc= ;;
    (--mpc|--mpd) spotify= ; mpc=mpc ;;
    (*) break ;;
  esac
  shift
done
test -n "$spotify$mpc" || findplayer
${spotify}${mpc}_ctl "$@"
