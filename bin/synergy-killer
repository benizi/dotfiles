#!/usr/bin/perl
use 5.016;
use warnings;
use IPC::Run 'run';
use Getopt::Long;
GetOptions(
	'kill|k' => \(my $just_kill = 1),
	'port=i' => \(my $port = 24800),
	'dry-run' => \(my $dry = 0),
) or die 'options';

sub lines {
	my $out;
	run \@_, '>', \$out, '2<&-';
	chomp(my @lines = split $/, $out);
	@lines
}

sub my_kill {
	return unless @_;
	my @cmd = (qw/sudo kill -9/, @_);
	unshift @cmd, 'echo' if $dry;
	say for lines @cmd;
}

my %ppids;
{
	my @pstree = reverse lines qw/pstree -aclpu/;
	my $max;
	my $seen = 0;
	for (@pstree) {
		my ($pref, $proc, $pid) = /^([\s\|\-\`]*)([^\s,]+),(\d+)/;
		next unless defined $pid;
		($seen, $max) = (1, length $pref) if $pid == $$;
		next unless $seen;
		next unless $max > length $pref;
		$ppids{$pid}++;
		$max = length $pref;
	}
}

if ($just_kill) {
	my @lsof = qw/sudo lsof -Fp/;
	push @lsof, -i => ":$port";
	push @lsof, -c => $_ for qw/synergys synergyc/;
	my @procs = grep !$ppids{$_}, map s/^p// ? $_ : (), lines @lsof;
	my_kill @procs;
	my $tag = $dry ? '[DRY RUN] ' : '';
	my $es = @procs == 1 ? "" : "es";
	my $list = @procs ? ": @procs" : "";
	say $tag, "Killed ", 0 + @procs, " old Synergy process", $es, $list;
}
