#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long qw/:config pass_through/;
use Tie::IxHash;
tie my %opts, 'Tie::IxHash';
use File::Spec;
GetOptions(
	'widthtype=s' => \($opts{'--widthtype'} = 'request'),
	'heighttype=s' => \($opts{'--heighttype'} = 'pixel'),
	'height=i' => \($opts{'--height'} = '24'),
	'edge=s' => \($opts{'--edge'} = 'bottom'),
	'align=s' => \($opts{'--align'} = 'right'),
	'margin=i' => \($opts{'--margin'} = '0'),
	'distance=i' => \($opts{'--distance'} = '32'),
	'dry|dry-run' => \(my $dry_run = 0),
) or die 'options';
my @cmd;
my $info0 = join ':', (stat $0)[0,1];
my $found_trayer = 0;
for my $dir (File::Spec->path) {
	my $trayer = File::Spec->catfile($dir, 'trayer');
	next unless -f $trayer;
	next if $info0 eq join ':', (stat $trayer)[0,1];
	$found_trayer = 1;
	push @cmd, $trayer;
	last;
}
$found_trayer or die "Could not find trayer\n";
push @cmd, map {; $_, $opts{$_} } keys %opts;
if ($dry_run) {
	print "@cmd\n";
} else {
	exec { $cmd[0] } 'trayer', @cmd[1..$#cmd];
}
__END__
trayer --widthtype request --heighttype pixel --height 24 --edge bottom --align right --margin 0 --distance 32
