#!/bin/zsh

bt-is-powered || exit 0

for nodeattr in $(
  bluez-dbus hci0 org.freedesktop.DBus.Introspectable.Introspect |
  xmllint --xpath '/node/node/@name' -)
do
  dev="${${nodeattr#*\"}%\"}"
  devprops="$(bluez-dbus hci0/$dev \
    org.freedesktop.DBus.Properties.GetAll \
    string:org.bluez.Device1)"
  for line in ${(f)devprops}
  do
    case $line in
      ('         Address '*')') btaddr=${${=line}[-2]} ;;
      ('         Connected '*) connected=${line##* } ;;
      ('         Icon '*')') icon=${${=line}[-2]} ;;
      (*-0000-*) profiles=$line ;;
    esac
  done
  # Filter for "Audio Sink" BT Profile
  (( ${profiles[(I)0000110b-0000-1000-8000-00805f9b34fb]} )) || continue
  # And something audio-related (filter phones and computers)
  [[ $icon = (*audio*) ]] || continue
  printf '%s\t%s\n' $btaddr $connected
done
