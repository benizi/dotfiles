#!/usr/bin/env perl
use 5.016;
use open ':std', ':utf8';
use utf8;

use Encode qw/decode_utf8/;
use File::Find;
use File::Spec;
use Getopt::Long qw/:config pass_through/;
use POSIX qw/getcwd/;

my $line_drawing = 1;

# s_ = sub, k_ = kid, mid = not last, end = last
my @drawing = qw/
  s_end        
  s_mid |   │  
  k_mid |-- ├──
  k_end `-- └──
/;

@ARGV = map { decode_utf8($_, 1) } @ARGV;

my %unimplemented_flags;
my $pattern = qr/./;
GetOptions(
  'a' => \(my $list_all = 0),
  'l' => \(my $follow = 0),
  'P=s' => sub {
    shift;
    local $_ = shift;
    s/^\*//;
    s/\*$//;
    $pattern = $_
  },
  'ignore-case' => \(my $ignore_case = 0),
  'prune' => \(my $prune = 0),
  (map { ($_, \$unimplemented_flags{$_}) }
    'C', # always colorize
    'I=s', # exclude pattern
    'matchdirs', # use -P for dirs too
    'noreport', # don't print final report
  ),
) or die 'options';

$pattern = qr/$pattern/i if $ignore_case;

my %tree;
my %anyfiles;
my $root = shift // '.';

find {
  wanted => sub {
    return if $_ eq '.';
    my $rel = File::Spec->abs2rel($File::Find::dir, $File::Find::topdir);
    $rel = $rel eq '.' ? '' : "/$rel";
    return if /\A\./ and not $list_all;
    return unless -d or /$pattern/;
    $tree{"$rel/$_"} //= [] if -d;
    unless (-d or $anyfiles{$rel}++) {
      my @parts = split '/', "/$rel", -1;
      $anyfiles{join '/', @parts[0..$_]}++ for 0..$#parts;
    }
    my $name = -l ? "$_ -> ".(readlink($_)//"(ENOPERM)") : $_;
    push @{$tree{$rel}}, $name;
  },
  follow => $follow,
}, $root;

my %chars;
while (@drawing) {
  my ($name, @versions) = splice @drawing, 0, 3;
  for ($versions[$line_drawing]) {
    y/\xa0/ /;
    s/$/ /;
    $chars{$name} = $_;
  }
}

sub print_tree {
  my ($prefix, $location) = (@_, "", "");
  my @kids = sort @{$tree{$location}};
  while (@kids) {
    my $kid = shift @kids;
    my $pos = @kids ? 'mid' : 'end';
    my $kid_pre = $chars{"k_$pos"};
    my $sub_pre = $chars{"s_$pos"};
    say $prefix, $kid_pre, $kid;
    my $sub = "$location/$kid";
    next unless exists $tree{$sub};
    print_tree($prefix . $sub_pre, $sub);
  }
}

if ($prune) {
  for (grep !$anyfiles{$_}, keys %tree) {
    delete $tree{$_};
    next unless my ($dir, $file) = m{^(.*)/([^/]+)$};
    next unless exists $tree{$dir};
    @$_ = grep $_ ne $file, @$_ for $tree{$dir};
  }
}

say $root;
print_tree;
