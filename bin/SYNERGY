#!/bin/zsh

# keep track of all spawned processes
typeset -a kids hosts
unset added_self
self="$(hostname)"

for arg
do
  if [[ $arg = . ]] ; then
    added_self=true
    hosts+=( $self )
  else
    hosts+=( $arg )
  fi
done

(( $+added_self )) || hosts=( $self $hosts )

# set up a Synergy server for the current host and others listed as args
synergys -f -a 127.0.0.1 -c =(synergyk $hosts) &
kids+=( $! )

trap 'kill $kids' INT

fastssh() {
  ssh -c chacha20-poly1305@openssh.com,blowfish-cbc $@
}

# run synergy-killer on each remote host, forwarding port 24800 to localhost
for host in ${(R)hosts/$self}
do
  fastssh $host 'synergy-killer -k'
  fastssh -R 24800:localhost:24800 $host 'synergy-killer -c' &
  kids+=( $! )
done

wait $kids
