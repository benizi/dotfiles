#!/bin/zsh

set -e

in=~/Downloads
: ${out=/downloads}

if (( $# ))
then
  specified=true
  : ${rmjson=false}
else
  specified=false
  : ${rmjson=true}
  set -- $in/photos*.json(Nom)
fi

for json
do
  jq -r .url $json |
  url2json |
  jq -r '"\(.host)\(.path)"' |
  sed 's/^www\.//' |
  awk -F/ '{$2="";$4="";NF--}1' |
  read site user id album

  test -d $out/$site || die "! -d $out/$site"
  meta=$out/$site/json
  base=$out/$site/$user/$album
  jout=$meta/$user.$id.$album.json

  mkdir -p $meta
  $specified || rsync --itemize-changes --info=stats0,flist0 -Pav $json $jout >&2

  jq -r '.photos|length' $json |
  read total
  jq -r '.photos[].urls.original' $json |
  nl |
  while read -r n url
  do
    dst=$out/$site/$user/$album/$(printf '%03d' $n)-${url##*/}
    echo down $url $dst $n
  done |
  sed -e '1idir '$meta |
  sed -e '1idir '$base |
  while read action url out i
  do
    case $action in
      (dir) printf 'mkdir -p "%s"\n' $url ;;
      (down)
        cat <<DL
printf '[%03d/%03d] %s %s \\n' $i $total "$user" "$album"
curl --progress-bar -C - -o "$out" "$url"
case \$? in
  (0) nodownload=false ;;
  (33) : ;;
  (*) hadfailure=true ;;
esac
DL
        case $i in
          ($total) : ;;
          (*) cat <<'CURSOR'
tput cuu 2 2>/dev/null || :
printf '\r'
CURSOR
        esac
    esac
  done |
  {
    echo 'hadfailure=false nodownload=true'
    cat
    ! $rmjson || printf '$hadfailure || $nodownload || rm "%s"\n' "$json"
  } |
  sed -e '1i{' -e '$a}' |
  sh -es
done
