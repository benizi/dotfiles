#!/usr/bin/perl
use strict;
use warnings;
use feature ':5.10';
use Getopt::Long qw/:config pass_through/;
my %opt;
my @xkbopts = qw/model layout variant keycodes/;
GetOptions(
	(map {; "$_=s" => \$opt{$_} } @xkbopts),
	'reset=s' => \$opt{reset},
	'option=s@' => \($opt{options}=[]),
	'menu' => \(my $menu = 0),
	'dry' => \(my $dry_run = 0),
	'extra=s@' => \my @extra,
) or die 'options';
sub run_it { if ($dry_run) { print "@_\n"; } else { system { $_[0] } @_; } }
my %defaults = (
	'' => {
		model => 'pc104',
		layout => 'benizi',
		variant => 'intl',
		keycodes => 'evdev',
		reset => 1,
		options => [
			qw/lv3:ralt_switch altwin:super_win compose:menu/
#			qw/lv3:ralt_switch compose:menu/
		],
	},
);
my @defopt = qw/layout variant/;
for (@defopt) { $opt{$_} //= shift @ARGV if @ARGV; }
push @extra, @ARGV if @ARGV;
warn "Extra options: (@extra)\n" if @extra;

my $path = $opt{layout} || '';
my $sep = '.';
do {
	for my $k (keys %{$defaults{''}}) {
		next unless exists $defaults{$path}{$k};
		my $v = $defaults{$path}{$k};
		if ($v and ref($v)~~'ARRAY') {
			push @{$opt{$k}}, @$v;
		} else {
			$opt{$k} //= $v;
		}
	}
} while $path =~ s/^((?:(?!\Q$sep\E).)+)(?=\Q$sep\E|$)//;
if ($menu) {
	for ($opt{options}) {
		@$_ = grep !/^compose:menu/, grep !/^altwin/, @$_;
		push @$_, 'altwin:menu';
	}
}
my @cmd = ('setxkbmap');
push @cmd, map {; "-$_" => $opt{$_} } grep length($opt{$_}), @xkbopts;
push @cmd, -option => '' if $opt{reset};
push @cmd, -option => $_ for @{$opt{options}};
push @cmd, @extra;
run_it @cmd;
run_it 'xmodmap', "$ENV{HOME}/.xmodmap";
__END__
setxkbmap
-model pc104
-layout us
-variant intl
-option ''
-option lv3:ralt_switch
-option altwin:super_win
-option ctrl:nocaps
-option compose:menu
