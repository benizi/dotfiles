#!/usr/bin/perl
use strict;
use warnings;
use feature ':5.10';
use FindBin '$Bin';
use Getopt::Long qw/:config pass_through/;
use IPC::Run;
my (%opt, %args);
my @xkbopts = qw/rules model layout variant keycodes/;
my @default_options = qw/terminate:ctrl_alt_bksp/;

sub version {
	local @ARGV = grep -f, map "/sys/devices/virtual/dmi/id/${_}_version", @_;
	do { undef local $/; (@ARGV ? <> : "") }
}

my $is_macbook = (version 'board') =~ /MacBook/;
push @default_options, 'benizi:x240' if (version 'product') =~ /X240/;

my $xkb_dir = "$Bin/../xkb";

GetOptions(
	(map {; "$_=s" => \$opt{$_} } @xkbopts),
	'o|option=s@' => \my @options,
	'dry' => \(my $dry_run = 0),
	'winkey=s' => \(my $win = 'mod4'),
	'modmap=s@' => \my @modmaps,
	'M|justmod' => \(my $just_modmap = 0),
	'clear' => \(my $clear_mods),
	'I=s@' => \my @includes,
	'macbook' => \$is_macbook,
	'print' => \(my $just_print),
	'compile!' => \(my $run_xkbcomp = 1),
	'setxkbmap|Wp=s@' => \($args{setxkbmap} = []),
	'xkbcomp|Wc=s@' => \($args{xkbcomp} = []),
) or die 'options';

@includes = grep -d, $xkb_dir unless @includes;
@modmaps = grep -f, "$xkb_dir/.xmodmap" unless @modmaps;

{
	my $tty;
	sub tty {
		return $tty if $tty;
		open $tty, '>', '/dev/tty' or $tty = \do { my $stringio = '' };
		$tty;
	}
}

sub terminal_out {
	scalar grep -t, *STDOUT, *STDERR;
}

sub all_piped {
	scalar !grep !-p, *STDOUT, *STDERR;
}

sub want_errors {
	terminal_out and not all_piped;
}

sub run_it {
	my @flat = map ref() ? @$_ : $_, grep { (!ref) or ('ARRAY' eq ref) } @_;
	$_ = (length) ? $_ : "''" for @flat;
	my $fh = $dry_run ? *STDOUT : *STDERR;
	stat $fh;
	$fh = tty if $dry_run and not (-f _ or -p _);
	say $fh "@flat" if $dry_run or want_errors;
	$dry_run and return;
	@_ = [@_] unless grep ref, @_;
	&IPC::Run::run;
}
push @options, @default_options;
my %defaults = (
	rules => 'benizi',
	model => 'pc104',
	layout => 'benizi',
	variant => 'intl',
	keycodes => 'evdev',
);

for (qw/layout variant/) { $opt{$_} //= shift @ARGV if @ARGV; }
push @{$args{setxkbmap}}, splice @ARGV;
$opt{$_} //= $defaults{$_} for keys %defaults;

for (sort keys %args) {
	my $val = $args{$_};
	next unless @$val;
	warn "Extra $_ options:\n", map "  $_\n", @$val;
}

sub includes {
	map "-I$_", @includes;
}

sub xkbcomp {
	my @dest = $just_print ? qw/-xkb -o -/ : $ENV{DISPLAY};
	my @pipe = grep !want_errors, '2>', sub {};
	[qw/xkbcomp -w 3/, includes, @{$args{xkbcomp}}, '-', @dest], @pipe;
}

sub setxkbmap {
	my @cmd = ('setxkbmap', includes);
	push @cmd, map {; "-$_" => $opt{$_} } grep length($opt{$_}), @xkbopts;
	push @cmd, -option => $_ for @options;
	push @cmd, @{$args{setxkbmap}};
	push @cmd, '-print' if $just_print or $run_xkbcomp;

	@cmd = ([@cmd], '|', xkbcomp) if $run_xkbcomp;

	run_it @cmd;
}

sub xmodmap_e {
	run_it xmodmap => -e => "@_";
}

sub swap_mods {
	my ($key, $old, $new) = @_;
	xmodmap_e "remove $old = $key";
	xmodmap_e "add $new = $key";
}

sub xmodmap_files {
	run_it xmodmap => $_ for @modmaps;
}

sub macbook_mousekeys {
	my ($b2, $b3) = (64, 108);
	xmodmap_e "keycode $b2 = Pointer_Button2";
	xmodmap_e "keycode $b3 = Pointer_Button3";
	run_it xkbset => 'm';
}

sub windows_key {
	swap_mods 'Super_L', sort { ($a eq $win) <=> ($b eq $win) } qw/mod3 mod4/;
}

sub system_key {
	$is_macbook ? macbook_mousekeys : windows_key;
}

sub alt_is_meta {
	swap_mods 'Alt_R', 'mod1', 'mod3';
}

sub clear_mods {
	run_it xdotool => keyup => $_
		for qw/
		Hyper_L Hyper_R
		Super_L Super_R
		Alt_L Alt_R
		Control_L Control_R
		Shift_L Shift_R
		/;
}

sub main {
	setxkbmap unless $just_modmap;
	return if $just_print;
	xmodmap_files;
	system_key;
	alt_is_meta;
	clear_mods if $clear_mods;
}

main

__END__
setxkbmap
-model pc104
-layout us
-variant intl
-option ''
-option lv3:ralt_switch
-option altwin:super_win
-option ctrl:nocaps
-option compose:menu
