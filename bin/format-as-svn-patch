#!/usr/bin/perl
use strict;
use warnings;
use bytes;
#use open ':std', ':bytes';
binmode STDIN;
binmode STDOUT;

use Getopt::Long qw/:config pass_through/;
GetOptions(
	'filename=s' => \(my $filename = 'changed-files'),
	'revision=i' => \(my $revision = 0),
	'force' => \(my $force = 0),
) or die 'options';

my $content = do { undef local $/; <> };
my $maybe_binary = $content =~ /[^\t\r\n\x20-\x7e]/;
if (-t 0 and -t 2 and $maybe_binary and not $force) {
	warn "File might be binary.  Run anyway?\n";
	chomp(my $yn = <STDIN>);
	die "Not outputting binary diff\n" unless $yn =~ /^y/i;
}

my $lines = ($content =~ s/((?:\r(?!\n)|\r?\n(?!\Z)))/$1+/g);
my $no_newline = $content !~ /\n$/;
$lines++ if $no_newline;

my $tab = "\t";
print <<HEADER;
Index: $filename
===================================================================
--- $filename$tab(revision $revision)
+++ $filename$tab(revision $revision)
\@\@ -0,0 +1,$lines \@\@
+$content
HEADER
$no_newline and print "\\ No newline at end of file\n";
if ($maybe_binary) {
	print <<BINARY;

Property changes on: $filename
___________________________________________________________________
Added: svn:mime-type
   + application/octet-stream

BINARY
}
