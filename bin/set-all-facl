#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long;
my @perms;
my @web_users = qw/wwwrun apache httpd lighttpd nginx/;
my @args;
my $permbits;
GetOptions(
	'permbit=s' => \$permbits,
	'n|dry-run' => \(my $dry = 0),
	'verbose+' => \(my $verbose = 0),
	'defaults!' => \(my $defaults = 1),
	'sudo!' => \(my $do_sudo = 1),
	'recurse|recursive|R!' => \(my $recursive = 0),
	'u|user' => \(my $do_current_user = 0),
	'g|group' => \(my $do_current_group = 0),
	'w|webusers|webservers' => \(my $do_web_users = 0),
	'<>' => sub {
		for (@_) {
			/^[dug]:/ ? (push @perms, $_) :
			/^o::/ ? (push @perms, $_) :
			(/^:(...)$/ or /^([r\-][w\-][x\-])$/i) ? ($permbits = $1) :
			push @args, $_;
		}
	},
) or die 'options';
push @perms, grep $do_current_user, "u:".getpwuid $<;
push @perms, grep $do_current_group, "g:".getgrgid $(;
if ($do_web_users) {
	my @web_perms = map "u:$_", grep scalar getpwnam $_, @web_users;
	push @perms, map "$_:".($permbits // 'r-X'), @web_perms;
}
@perms = map {; ("d:$_", $_) } @perms if $defaults;
$permbits //= 'rwX';
/^(?:d:.*:.*:.*|[ug]:.*:.*|o::...)$/ or $_ .= ":$permbits" for @perms;
@perms or die "No default/user/group permissions specified\n";
@args or die "No files/directories specified\n";
my (@with_defaults, @no_defaults);
(-d) ? (push @with_defaults, $_) : (push @no_defaults, $_) for @args;
for (
	[ 1, @no_defaults ],
	[ 0, @with_defaults ],
) {
	my ($filter_defaults, @paths) = @$_;
	next unless @paths;
	my @use_perms = @perms;
	@use_perms = grep !/^d:/, @use_perms if $filter_defaults;
	my @cmd = (
		setfacl =>
		grep($recursive, '-R'),
		-m => join(',', @use_perms),
		@paths
	);
	$< and $do_sudo and @cmd = ( sudo => @cmd );
	if ($dry or $verbose) {
		print "[$_]=$cmd[$_]\n" for 0..$#cmd;
	}
	$dry or system { $cmd[0] } @cmd;
}
