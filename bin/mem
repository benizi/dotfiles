#!/bin/sh
test -e /proc/meminfo || exit 0
awk '
{ stats[substr($1,1,length($1)-1)] = int($2/1024) }
END {
  ram_tot = stats["MemTotal"]
  tot = ram_tot + stats["SwapTotal"]
  ram_free = stats["MemFree"]
  free = ram_free + stats["SwapFree"]
  used = tot - free
  avail = stats["MemAvailable"]
  if (ram_tot > 20000) low = ram_free < 4000
  else if (ram_tot < 1000) low = 10 * avail < tot
  else low = free < 2000 || avail < 300
  warn = (low && avail < 1000)
  mark = warn ? "!!! " : ""
  print mark "mem:" avail "/" used "/" tot
}
' /proc/meminfo
